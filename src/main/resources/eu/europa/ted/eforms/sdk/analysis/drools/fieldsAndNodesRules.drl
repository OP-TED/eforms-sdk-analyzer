package eu.europa.ted.eforms.sdk.analysis.drools;

unit SdkUnit;

import eu.europa.ted.eforms.sdk.domain.field.Field;
import eu.europa.ted.eforms.sdk.domain.field.XmlStructureNode;

rule "Fields.json has correct SDK version"
when
  /fieldsAndNodesMetadata[ $f: this, $version: sdkVersion, $version != "eforms-sdk-" + sdkMetadata.version ]
then
  results.add(new ValidationResult($f, "The value " + $version + " in sdkVersion is incorrect", ValidationStatusEnum.ERROR));
end

rule "Every node has at least one child"
when
  /nodes[ $n: this, $nodeId : id ]
  not (exists /nodes[ parentId == $nodeId ] or /fields[ parentId == $nodeId ])
then
  results.add(new ValidationResult($n, "Node has no child node or field", ValidationStatusEnum.ERROR));
end

rule "Every node parent ID corresponds to an existing node"
when
  /nodes[ $n: this, $parentId : parentId, parentId != null ]
  not (exists /nodes[ id == $parentId ])
then
  results.add(new ValidationResult($n, "Node parent ID " + $parentId + " does not correspond to an existing node", ValidationStatusEnum.ERROR));
end

rule "Every field parent ID corresponds to an existing node"
when
  /fields[ $f: this, $parentId : parentId ]
  not (exists /nodes[ id == $parentId ])
then
  results.add(new ValidationResult($f, "Field parent ID " + $parentId + " does not correspond to an existing node", ValidationStatusEnum.ERROR));
end

rule "There are no loops in the node structure"
when
  $ancestorsId : /nodes[ $n: this, $nodeId : id ]/ancestors/id
  exists /nodes[ id == $nodeId, id == $ancestorsId ]
then
  results.add(new ValidationResult($n, "Node is part of its ancestors (loop in the node hierarchy)", ValidationStatusEnum.ERROR));
end

// TEDEFO-2205: Xpath absolute uniqueness is verified.
//  For fields.json file:
//    The xpathAbsolute of a field should not be equal to the xpathAbsolute of a node
rule "Xpath absolute uniqueness is verified"
when
  /fields[ $f: this, $fieldId : id, $xpathAbsoluteField: xpathAbsolute ];
  /nodes[ $n: this, $nodeId : id, $xpathAbsoluteNode: xpathAbsolute ]
  eval($xpathAbsoluteField == $xpathAbsoluteNode)
then
  results.add(new ValidationResult($f, "xpath absolute are equal for field " + $fieldId + ", node " + $nodeId, ValidationStatusEnum.ERROR));
end

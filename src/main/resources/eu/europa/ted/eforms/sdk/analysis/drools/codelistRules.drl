package eu.europa.ted.eforms.sdk.analysis.drools;

unit SdkUnit;

import eu.europa.ted.eforms.sdk.domain.codelist.Codelist;

rule "Codes in tailored codelists are present in parent codelist"
when
  $tailoredcode : /codelists[ $tailored: this, $parentId: parentId, isTailored == true ]/codes
  not (exists /codelists[ id == $parentId, codes contains $tailoredcode ])
then
  results.add(new ValidationResult($tailored, "Code " + $tailoredcode + " is not present in parent codelist " + $parentId, ValidationStatusEnum.ERROR));
end

rule "Codelist indicated in codelists index exists"
when
  $codelistId : /codelistsIndex[ $cli: this ]/indexItems.id
  not (exists /codelists[ id == $codelistId ])
then
  results.add(new ValidationResult($cli, "Codelist with id " + $codelistId + " is not present the codelists folder", ValidationStatusEnum.ERROR));
end

rule "Codelist files are indicated in codelists index"
when
  $codelistId : /codelists[ $cl: this ]/id
  not (exists /codelistsIndex/indexItems[ id == $codelistId ])
then
  results.add(new ValidationResult($cl, "Codelist file with id " + $codelistId + " is not listed in codelists.json", ValidationStatusEnum.ERROR));
end

